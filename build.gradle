plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id "org.sonarqube" version "4.4.1.3373"
}

group = "eu.nonstatic"
version = "1.2.0"

repositories {
    maven {
        url = "https://maven.pkg.github.com/nonstaticeu/audiolib"
        credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
    mavenLocal()
    mavenCentral()
}

def lombokVersion = '1.18.30'
def junitVersion = '5.10.1'

dependencies {
    annotationProcessor("org.projectlombok:lombok:$lombokVersion")
    implementation('com.ibm.icu:icu4j:73.2')
    implementation('org.slf4j:slf4j-api:2.0.7')
    implementation('eu.nonstatic:audiolib:0.2.0')
    compileOnly("org.projectlombok:lombok:$lombokVersion")

    testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
}

configurations.implementation {
    exclude group: 'org.slf4j', module: 'slf4j-simple'
}


java {
    sourceCompatibility = JavaVersion.toVersion("11")
    targetCompatibility = JavaVersion.toVersion("11")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}


// use JUnit 5 platform
test {
    testLogging.showStandardStreams = true
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        mavenLocal()
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/nonstaticeu/cuelib"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
